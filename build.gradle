plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.flywaydb.flyway' version '11.0.1'
}

group = 'com.pedro'
version = '1.0.0-LTS'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.mysql:mysql-connector-j'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

flyway {
	url = project.hasProperty('DB_URL') ? project.DB_URL : 'jdbc:mysql://localhost:3306/event_ticket_exchange'
    user = project.hasProperty('DB_USER') ? project.DB_USER : 'root'
    password = project.hasProperty('DB_PASSWORD') ? project.DB_PASSWORD : 'root'
	locations = ['filesystem:./src/main/resources/db/migration']
}

tasks.named('test') {
    useJUnitPlatform()
}

task migrate(type: Exec) {
    description = 'Executa o comando flyway migrate'
    group = 'Flyway'

    def dbUrl = flyway.url
    def dbUser = flyway.user
    def dbPassword = flyway.password
    def dbLocations = flyway.locations[0]

    def flywayPath = '/usr/local/bin/flyway'

    def command = [
        flywayPath, 
        'migrate', 
        "-url=${dbUrl}", 
        "-user=${dbUser}", 
        "-password=${dbPassword}", 
        "-locations=${dbLocations}"
    ]

    commandLine command
}

